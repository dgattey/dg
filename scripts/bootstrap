#!/usr/bin/env zsh
set -euo pipefail

ROOT_DIR="$(cd "$(dirname "${(%):-%x}")/.." && pwd)"

echo "Bootstrapping repo in ${ROOT_DIR}"

# Check Homebrew
command -v brew >/dev/null 2>&1 || {
  echo "Homebrew required. Install from https://brew.sh/" >&2
  exit 1
}

# Install system dependencies
echo "Installing system dependencies..."
brew bundle --file="${ROOT_DIR}/Brewfile"

# Authenticate 1Password
export OP_ACCOUNT="my.1password.com"
echo "Authenticating with 1Password..."
op whoami >/dev/null 2>&1 || op signin --account "$OP_ACCOUNT"

# Set up Node
echo "Setting up Node..."
export PATH="${HOME}/.nodenv/bin:${PATH}"
eval "$(nodenv init -)"

NODE_VERSION="$(cat "${ROOT_DIR}/.node-version")"
echo "Using Node ${NODE_VERSION}"
nodenv install -s "$NODE_VERSION"
nodenv local "$NODE_VERSION"
nodenv rehash

# Set up pnpm
echo "Setting up pnpm..."
corepack enable
PNPM_VERSION="$(node -e "console.log(require('./package.json').packageManager.match(/pnpm@([^+]+)/)[1])")"
corepack prepare "pnpm@${PNPM_VERSION}" --activate

# Install Turbo
if ! command -v turbo >/dev/null 2>&1; then
  echo "Installing Turbo CLI..."
  npm -g install turbo
  nodenv rehash
fi

# Install dependencies and generate .env
cd "$ROOT_DIR"
pnpm install
pnpm --filter=@dg/cli env

# Set up direnv to auto-load .env (for TURBO_TOKEN, etc.)
if [[ ! -f ".envrc" ]]; then
  echo "dotenv" > .envrc
fi

# Add direnv hook to .zshrc if missing
if ! grep -q "direnv hook" ~/.zshrc 2>/dev/null; then
  echo "" >> ~/.zshrc
  echo '# direnv hook (added by dg bootstrap)' >> ~/.zshrc
  echo 'eval "$(direnv hook zsh)"' >> ~/.zshrc
  echo "Added direnv hook to ~/.zshrc"
fi

# Activate direnv for this session
eval "$(direnv hook zsh)"
direnv allow .

# Configure Turbo remote caching (only if not already configured)
if [[ ! -f ".turbo/config.json" ]]; then
  # Source .env for TURBO_TEAM
  if [[ -f ".env" ]]; then
    set -a
    source .env
    set +a
  fi
  
  if [[ -n "${TURBO_TEAM:-}" ]]; then
    echo "Configuring Turbo remote caching for team: $TURBO_TEAM"
    mkdir -p .turbo
    echo "{\"teamId\":\"$TURBO_TEAM\"}" > .turbo/config.json
  else
    echo "Warning: TURBO_TEAM not set, skipping Turbo remote cache config"
  fi
fi

echo "Bootstrap complete."
