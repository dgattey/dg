#!/usr/bin/env zsh
set -euo pipefail

ROOT_DIR="$(cd "$(dirname "${(%):-%x}")/.." && pwd)"

echo "Bootstrapping repo in ${ROOT_DIR}"

# Check Homebrew
command -v brew >/dev/null 2>&1 || {
  echo "Homebrew required. Install from https://brew.sh/" >&2
  exit 1
}

# Install system dependencies
echo "Installing system dependencies..."
for pkg in nodenv node-build 1password-cli neonctl; do
  brew list --formula "$pkg" >/dev/null 2>&1 || brew install "$pkg"
done

# Authenticate 1Password
export OP_ACCOUNT="my.1password.com"
echo "Authenticating with 1Password..."
op whoami >/dev/null 2>&1 || op signin --account "$OP_ACCOUNT"

# Set up Node
echo "Setting up Node..."
export PATH="${HOME}/.nodenv/bin:${PATH}"
eval "$(nodenv init -)"

NODE_VERSION="$(cat "${ROOT_DIR}/.node-version")"
echo "Using Node ${NODE_VERSION}"
nodenv install -s "$NODE_VERSION"
nodenv local "$NODE_VERSION"
nodenv rehash

# Set up pnpm
echo "Setting up pnpm..."
corepack enable
PNPM_VERSION="$(node -e "console.log(require('./package.json').packageManager.match(/pnpm@([^+]+)/)[1])")"
corepack prepare "pnpm@${PNPM_VERSION}" --activate

# Install Turbo
if ! command -v turbo >/dev/null 2>&1; then
  echo "Installing Turbo CLI..."
  npm -g install turbo
  nodenv rehash
fi

# Authenticate Turbo
cd "$ROOT_DIR"
echo "Authenticating Turbo..."
turbo login --force && turbo link -y

# Install dependencies and generate .env
pnpm install
scripts/generate-env

echo "Bootstrap complete."
