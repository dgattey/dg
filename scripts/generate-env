#!/usr/bin/env zsh
set -euo pipefail

ROOT="$(cd "$(dirname "${(%):-%x}")/.." && pwd)"
OUTPUT_FILE="${ROOT}/.env"

# Skip if .env already exists
[[ -f "$OUTPUT_FILE" ]] && { echo ".env already exists" >&2; exit 0; }

# Check 1Password auth
export OP_ACCOUNT="my.1password.com"
if ! op whoami >/dev/null 2>&1; then
  echo "Signing in to 1Password..." >&2
  op signin --account "$OP_ACCOUNT" || {
    echo "1Password sign-in failed. Enable: 1Password > Settings > Developer > Integrate with 1Password CLI" >&2
    exit 1
  }
fi

# Write defaults
echo "Writing defaults..." >&2
grep -v '^#' "${ROOT}/config/env.defaults" | grep -v '^$' > "$OUTPUT_FILE"

# Fetch secrets in parallel
echo "Fetching secrets..." >&2
TMP_DIR="$(mktemp -d)"
trap "rm -rf '$TMP_DIR'" EXIT

while read -r key; do
  [[ -z "$key" || "$key" == \#* ]] && continue
  (op read "op://dg/${key}/value" > "${TMP_DIR}/${key}" 2>&1) &
done < "${ROOT}/config/env.secrets.keys"
wait

# Append secrets
for file in "$TMP_DIR"/*; do
  [[ -f "$file" ]] || continue
  key="$(basename "$file")"
  value="$(cat "$file")"
  
  if [[ -z "$value" || "$value" == *"ERROR"* ]]; then
    echo "Failed to read $key from 1Password" >&2
    exit 1
  fi
  
  # Escape for .env format
  value="${value//\\/\\\\}"
  value="${value//\"/\\\"}"
  value="${value//$'\n'/\\n}"
  
  echo "${key}=\"${value}\"" >> "$OUTPUT_FILE"
done

echo "Generated .env"
